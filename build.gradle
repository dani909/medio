import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'java'
apply plugin: 'scala'

group 'de.dani09'
version '1.0'

sourceCompatibility = 1.8

sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []
sourceSets.test.scala.srcDir "src/test/java"
sourceSets.test.java.srcDirs = []

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    implementation 'de.mediathekview:MLib:3.0.2'
    implementation 'org.scala-lang:scala-library:2.12.7'
    implementation 'org.json:json:20180813'
    implementation 'me.tongfei:progressbar:0.7.2'
    implementation 'com.github.scopt:scopt_2.12:3.7.0'
    implementation 'de.dani09:dani-http:0.4.1'
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compileClasspath.files.collect { "$it.name" }.join(' '),
                'Main-Class': 'de.dani09.moviedownloader.Main'
        )
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task createProperties {
    doLast {
        def version = project.version.toString()
        def path = Paths.get("$buildDir/resources/main/version.txt")
        Files.createDirectories(path.getParent())
        def file = new File(path as String)
        file.write(version)
    }
}

jar {
    dependsOn createProperties
}